<?xml version="1.0" encoding="utf-8"?>
<s:TitleWindow xmlns:fx="http://ns.adobe.com/mxml/2009"
			   xmlns:s="library://ns.adobe.com/flex/spark"
			   xmlns:mx="library://ns.adobe.com/flex/mx"
			   xmlns:video="com.avManager.view.video.*"
			   xmlns:detail="com.avManager.view.video.detail.*"
			   width="1200" height="618" close="onCloseHandler(event)"
			   creationComplete="onCreationCompleted(event)" title="车辆详情">
	<fx:Script>
		<![CDATA[
			import com.avManager.API;
			import com.avManager.Net;
			import com.avManager.model.Config;
			import com.avManager.model.data.Actress;
			import com.avManager.model.data.Video;
			import com.avManager.view.actress.container.ActressCell;
			import com.avManager.view.actress.selector.ActressSelector;
			
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			import org.libra.flex.utils.NativeProcessUtil;
			import org.libra.flex.utils.PopUpUtil;
			import org.libra.utils.DateUtil;
			
			private var _video:com.avManager.model.data.Video;
			
			public function get video():com.avManager.model.data.Video
			{
				return _video;
			}

			public function set video(value:com.avManager.model.data.Video):void
			{
				_video = value;
				if(value) {
					renderVideo();
				}
			}
			
			private function onCloseHandler(event:CloseEvent = null):void {
				PopUpUtil.instance.removePopUp(this, 1);
			}

			protected function onCreationCompleted(event:FlexEvent):void
			{
				renderVideo();
			}
			
			private function renderVideo():void{
				if(this.initialized){
					_titleLabel.text = _video.name;
					_codeLabel.text = _video.code;
					_dateLabel.text = DateUtil.toString(_video.date);
					_scoreSlider.value = _video.score;
					_scoreLabel.text = _video.score.toString();
					_magnetContainer.videoCode = _video.code;
					
					if(_video.isDesired){
						_isDesiredImg.source = 'assets/desired.png';
					} else {
						_isDesiredImg.source = 'assets/unDesired.png';
					}
					
					var f:File;
					var imgName:String = File.separator + _video.code + File.separator + _video.code + "l.jpg";
					var pathArr:Array = Config.getVideoPath();
					for each(var p:String in pathArr) {
						f = new File(p + imgName);
						if(f.exists) {
							_img.source = f.nativePath;
							break;
						}
					}
					
					Net.instance.fetch(API.getActressedByID(_video.actress), this.onFetchActresses);
				}
			}
			
			private function onFetchActresses(actresses:Array):void{
				_actressContainer.removeAllElements();
				
				var actressCell:ActressCell;
				var actress:Actress;
				for each(var actressObj:Object in actresses) {
					actress = new Actress();
					actress.createWithJson(actressObj);
					actressCell = new ActressCell();
					actressCell.actress = actress;
					_actressContainer.addElement(actressCell);
				}
				
				Net.instance.fetch(API.getCategoryByID(_video.category), this.onFetchCategory);
			}
			
			private function onFetchCategory(categorys:Array):void{
				var s:String = "";
				for each(var category:Object in categorys) {
					s += category.name + " ";
				}
				_categoryLabel.text = s;
				
				Net.instance.fetch(API.getSeriesByID(_video.series), this.onFetchSeries);
			}
			
			private function onFetchSeries(series:Object):void{
				_seriesLabel.text = series.name;				
			}
			
			private function onOpen(evt:Event):void{
				var pathArr:Array = Config.getVideoPath();
				var f:File;
				for each(var p:String in pathArr) {
					f = new File(p + File.separator + video.code);
					if(f.exists && f.isDirectory) {
						NativeProcessUtil.runCMD("start", f.nativePath, "/max");		
						break;
					}
				}
			}
			
			private function onPlay(evt:Event):void{
				var pathArr:Array = Config.getVideoPath();
				var formatArr:Array = Config.getVideoFormat();
				var f:File;
				var exists:Boolean = false;
				for each(var p:String in pathArr) {
					for each(var format:String in formatArr){
						f = new File(p + File.separator + video.code + File.separator + video.code + "." + format);
						if(f.exists) {
							f.openWithDefaultApplication();
							exists = true;
							break;
						}
					}
					if(exists){
						break;
					}
				}
				if(!exists){
					Alert.show("没有找到车辆");
				}
			}
			
			protected function onAddActress(event:MouseEvent):void
			{
				var actressSelector:ActressSelector = new ActressSelector();
				actressSelector.onSelectedActressHandler = this.onSelectedActress;
				PopUpUtil.instance.addPopUp(actressSelector, this, true, 1);
			}
			
			private function onSelectedActress(actress:Actress):void{
				Net.instance.fetch(API.addActressToVideo(_video.id, actress.name), onAddedActress);
			}
			
			private function onAddedActress(actressObj:Object):void{
				var actress:Actress = new Actress();
				actress.createWithJson(actressObj);
				var actressCell:ActressCell = new ActressCell();
				actressCell.actress = actress;
				_actressContainer.addElement(actressCell);
				
				video.actress.push(actress.id);
			}
			
			protected function onScoreSliderChangeEnded(event:FlexEvent):void
			{
				var score:int = _scoreSlider.value;
				_scoreLabel.text = score.toString();
				Net.instance.fetch(API.modifyVideoScore(_video.id, score), this.onModifyVideoScore);
			}
			
			private function onModifyVideoScore(data:Object):void{
				// do nothing
			}
			
			protected function onDesiredImgClicked(event:MouseEvent):void
			{
				Net.instance.fetch(API.modifyVideoIsDesired(video.id, !video.isDesired), this.onModifyDesired);
			}
			
			private function onModifyDesired(data:Object):void{
				video.isDesired = !video.isDesired;
				video = video;
				if(video.isDesired){
					Alert.show("已将该车辆添加到愿望清单中");
				} else {
					Alert.show("已将该车辆从愿望清单中移除");
				}
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:HGroup width="100%" height="100%">
		<s:VGroup width="800" height="100%">
			<s:HGroup width="100%" height="36" verticalAlign="middle">
				<s:Label id="_titleLabel" width="100%" height="100%"/>
				<s:Image id="_isDesiredImg" width="32" height="32" click="onDesiredImgClicked(event)"
						 source="@Embed('assets/unDesired.png')"/>
			</s:HGroup>
			<s:Image id="_img" width="800" height="537"
					 source="@Embed('assets/defaultVideoCover.jpg')"/>
		</s:VGroup>
		<s:VGroup width="100%" height="100%" gap="4" horizontalAlign="left" paddingTop="6">
			<s:HGroup width="100%" height="18" textAlign="center" verticalAlign="middle">
				<s:Label width="70" fontWeight="bold" text="车牌号:" textAlign="left"/>
				<s:Label id="_codeLabel"/>
			</s:HGroup>
			<s:HGroup width="100%" height="18" textAlign="center" verticalAlign="middle">
				<s:Label width="70" fontWeight="bold" text="发车日期:" textAlign="left"/>
				<s:Label id="_dateLabel"/>
			</s:HGroup>
			<s:HGroup width="100%" height="18" textAlign="center" verticalAlign="middle">
				<s:Label width="70" fontWeight="bold" text="类别:" textAlign="left"/>
				<s:Label id="_categoryLabel"/>
			</s:HGroup>
			<s:HGroup width="100%" height="18" textAlign="center" verticalAlign="middle">
				<s:Label width="70" fontWeight="bold" text="系列:" textAlign="left"/>
				<s:Label id="_seriesLabel"/>
			</s:HGroup>
			<s:HGroup width="100%" height="18" paddingRight="6" textAlign="center"
					  verticalAlign="middle">
				<s:Label width="70" fontWeight="bold" text="评分:" textAlign="left"/>
				<s:HSlider id="_scoreSlider" width="100%" changeEnd="onScoreSliderChangeEnded(event)"
						   maximum="100"/>
				<s:Label id="_scoreLabel" width="22" text="300"/>
			</s:HGroup>
			<s:VGroup width="100%">
				<s:HGroup width="100%" verticalAlign="middle">
					<s:Label width="70" fontWeight="bold" text="女司机:" textAlign="left"/>
					<s:Button label="添加女司机" click="onAddActress(event)"/>
				</s:HGroup>
				<s:Scroller width="100%" height="296">
					<s:TileGroup id="_actressContainer" width="387" horizontalGap="4"
								 orientation="rows"/>	
				</s:Scroller>
			</s:VGroup>
			<detail:MagnetContainer id="_magnetContainer" width="100%" height="114"/>
			<s:HGroup width="100%" gap="60" horizontalAlign="center" verticalAlign="middle">
				<s:Button label="打开文件夹" click="onOpen(event)"/>
				<s:Button label="播放影片" click="onPlay(event)"/>
			</s:HGroup>
		</s:VGroup>
	</s:HGroup>
</s:TitleWindow>
