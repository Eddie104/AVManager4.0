<?xml version="1.0" encoding="utf-8"?>
<s:VGroup xmlns:fx="http://ns.adobe.com/mxml/2009"
		  xmlns:s="library://ns.adobe.com/flex/spark"
		  xmlns:mx="library://ns.adobe.com/flex/mx"
		  xmlns:video="com.avManager.view.video.*"
		  width="100%" height="100%" addedToStage="onAddedToStage(event)"
		  creationComplete="onCreationCompleted(event)">
	<fx:Script>
		<![CDATA[
			import com.avManager.API;
			import com.avManager.Net;
			import com.avManager.events.VideoEvent;
			import com.avManager.model.Config;
			import com.avManager.model.data.Video;
			
			import mx.collections.ArrayCollection;
			import mx.controls.Alert;
			import mx.events.CloseEvent;
			import mx.events.FlexEvent;
			
			import org.libra.flex.utils.PopUpUtil;
			import org.libra.utils.text.StringUtil;
			
			import spark.events.IndexChangeEvent;
			
			// 一页的数量
			private static const _COUNT:int = 27;
			
			private static var _totalCount:int = 0;
			
			[Bindable]
			private var _totalPage:int = 0;
			
			[Bindable]
			private var _curPage:int = 1;
			
			protected function onAddedToStage(event:Event):void
			{
				this.fetchVideo();
			}
			
			private function fetchVideo():void{
				var code:String = _codeInput.text;
				if(StringUtil.isNullOrEmpty(code)) {
					code = "!";
				}
				Net.instance.fetch(API.getVideo((_curPage - 1) * _COUNT, _COUNT, this._sortTypeComboBox.selectedIndex + 1, code), onFetchVideo);
			}
			
			private function onFetchVideo(data:Object):void{
				_totalCount = data.count;
				_totalPage = Math.ceil(_totalCount / _COUNT);
				
				var l:int = data.video.length;
				for(var i:int = 0; i < l; i++) {
					var v:com.avManager.model.data.Video = new com.avManager.model.data.Video();
					v.createWithJson(data.video[i]);
					this.renderVideoDetail(v, i);
				}
				for(i = l; i < _COUNT; i++){
					this.renderVideoDetail(null, i);
				}
				
				if(_totalCount == 0 && !StringUtil.isNullOrEmpty(_codeInput.text)){
					Alert.show("未获取到车辆信息，是否根据车牌:" + _codeInput.text + "去网络上搜索信息?", "提示", Alert.YES | Alert.NO, this, this.onSearchFromNetAlertClosed);			
				}
			}
			
			private function onSearchFromNetAlertClosed(evt:CloseEvent):void{
				if(evt.detail == Alert.YES){
					var loader:URLLoader = new URLLoader();
					loader.addEventListener(Event.COMPLETE, onLoadVideoHtml);
					loader.load(new URLRequest(Config.instance.webURL + "/" + _codeInput.text));
				}
			}
			
			private function onLoadVideoHtml(evt:Event):void{
				var loader:URLLoader = evt.target as URLLoader;
				loader.removeEventListener(Event.COMPLETE, onLoadVideoHtml);
				
				var html:String = loader.data;
				// <span style="color:#CC0000;">MOJ-305</span></p>
				var code:String = html.match(/<span style="color:#CC0000;">.*<\/span><\/p>/)[0].toString().replace('<span style="color:#CC0000;">', '').replace('<\/span><\/p>', '');
				// <h3>MOJ-305 淫語で誘うベロちゅう痴女 あなたのザーメン管理してあげるから勝手に射精したら許さないから… 波多野結衣</h3>
				var name:String = html.match(/<h3>.*<\/h3>/)[0].toString().replace(/<\/?h3>/g, '');
				var dateString:String = html.match(/\d{4}-\d{2}-\d{2}/)[0];
				var arr:Array = dateString.split("-");
				arr[1] = int(arr[1]) - 1;
				dateString = arr.join("-");
				// <span class="genre"><a href="https://www.javbus.me/genre/f">單體作品</a></span>
				var category:Vector.<String> = new Vector.<String>();
				arr = html.match(/<span class="genre">.*<\/span>/g);
				for each(var s:String in arr){
					if(s.indexOf('target="_blank" rel="nofollow"') == -1){
						category.push(s.replace(/<span class="genre"><a href=".*">/, "").replace("</a></span>", ""));
					}
				}
				// <p><span class="header">系列:</span> <a href="https://www.javbus.me/series/fc6">3日間滞在して、寝食を共にする超高級美女ソープ</a></p>
				var series:String = "undefined";
				arr = html.match(/<p><span class="header">系列:<\/span> <a href=.*<\/a><\/p>/);
				if(arr.length > 0){
					series = arr[0].toString().replace(/<p><span class="header">系列:<\/span> <a href=".*">/, "").replace("</a></p>", "");	
				}
				
				// <span class="genre" onmouseover="hoverdiv(event,'star_9oc')" onmouseout="hoverdiv(event,'star_9oc')">
				// <a href="https://www.javbus.me/star/9oc">西川ゆい</a>
				var actress:Vector.<String> = new Vector.<String>();
				arr = html.match(/<span class="genre" onmouseover.*\r*\n*\s*<a href=.*<\/a>/g);
				for each(s in arr){
					actress.push(s.replace(/<span class="genre" onmouseover.*\r*\n*\s*<a href=.*">/, "").replace("</a>", ""));
				}
				
				Net.instance.fetch(API.addVideo(code, name, dateString, actress, category, series), this.onAddedVideo);
			}
			
			private function onAddedVideo(videoObj:Object):void{
				onFetchVideo({
					count: 1,
					video: [videoObj]
				});
			}
			
			private function renderVideoDetail(video:com.avManager.model.data.Video, index:int):void{
				var videoDetail:VideoCell = _container.getElementAt(index) as VideoCell;
				if(videoDetail){
					videoDetail.video = video;
				}
			}
			
			protected function onCreationCompleted(event:FlexEvent):void
			{
				var videoCell:VideoCell = null;
				for(var i:int = 0; i < _COUNT; i++){
					videoCell = new VideoCell();
					_container.addElement(videoCell);
					videoCell.addEventListener(com.avManager.events.VideoEvent.VIDEO_EVENT, onVideoEvent);
				}
			}
			
			private function onVideoEvent(evt:com.avManager.events.VideoEvent):void{
				var videoDetail:VideoDetail = new VideoDetail();
				videoDetail.video = evt.video;
				PopUpUtil.instance.addPopUp(videoDetail, this, true, 1);
			}
			
			protected function onPrevPage(event:MouseEvent):void
			{
				if(this._curPage > 1){
					this._curPage--;
					fetchVideo();
				}
				
			}
			
			protected function onNextPage(event:MouseEvent):void
			{
				if(this._curPage < this._totalPage) {
					this._curPage++;
					fetchVideo();
				}
			}
			
			protected function onCodeSearch(event:MouseEvent):void
			{
				this._curPage = 1;
				fetchVideo();
			}
			
			protected function onSortTypeComboBoxChanged(event:IndexChangeEvent):void
			{
				this._curPage = 1;
				fetchVideo();				
			}
			
		]]>
	</fx:Script>
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:HGroup width="100%" height="26" verticalAlign="middle">
		<s:HGroup width="50%" height="100%" paddingLeft="6" verticalAlign="middle">
			<s:TextInput id="_codeInput"/>
			<s:Button label="搜索车号" click="onCodeSearch(event)"/>
			<s:ComboBox id="_sortTypeComboBox" width="86" change="onSortTypeComboBoxChanged(event)"
						initialize="_sortTypeComboBox.textInput.editable=false" selectedIndex="0">
				<s:ArrayCollection>
					<fx:String>按评分降序</fx:String>
					<fx:String>按评分升序</fx:String>
					<fx:String>按日期降序</fx:String>
					<fx:String>按日期升序</fx:String>
					<fx:String>按番号降序</fx:String>
					<fx:String>按番号升序</fx:String>
				</s:ArrayCollection>
			</s:ComboBox>
		</s:HGroup>
		<s:HGroup width="50%" height="100%" horizontalAlign="right" paddingRight="6"
				  verticalAlign="middle">
			<s:Button label="前一页" click="onPrevPage(event)"/>
			<s:Button label="后一页" click="onNextPage(event)"/>
			<s:Label height="100%" text="{this._curPage}/{this._totalPage}" verticalAlign="middle"/>
		</s:HGroup>
	</s:HGroup>
	<s:Scroller width="100%" height="100%">
		<s:TileGroup id="_container" width="100%" height="100%" paddingLeft="6" paddingRight="6"/>
	</s:Scroller>
</s:VGroup>
